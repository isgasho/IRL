// Test Global Value Numbering
// See Figure 12.19 of Whale Book

@g: i32

fn @test($n: i32) {
%B1:
    @g <- mov i32 1
    $i.1 <- mov i32 1
    $j.1 <- mov i32 1
    $k.1 <- mov i32 @g
    jmp %B2
%B2:
    $i.2 <- phi i32 [%B5: $i.5] [%B1: $i.1] // test order of phi operands
    $j.2 <- phi i32 [%B1: $j.1] [%B5: $j.5]
    $k.2 <- phi i32 [%B1: $k.1] [%B5: $k.5]
    $t <- mod i32 $i.2, 2
    $c <- eq i32 $t, 0
    br $c ? %B3 : %B4
%B3:
    $i.3 <- add i32 1, $i.2 // test operator commutativity
    $j.3 <- add i32 $j.2, 1
    $k.3 <- add i32 $k.2, 1
    jmp %B5
%B4:
    $i.4 <- add i32 $i.2, 3
    $j.4 <- add i32 $j.2, 3
    $k.4 <- add i32 $k.2, 3
    jmp %B5
%B5:
    $i.5 <- phi i32 [%B3: $i.3] [%B4: $i.4]
    $j.5 <- phi i32 [%B3: $j.3] [%B4: $j.4]
    $k.5 <- phi i32 [%B3: $k.3] [%B4: $k.4]
    $d <- gt i32 $j.5, $n
    br $d ? %B2 : %B6
%B6:
    ret
}